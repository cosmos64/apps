
pkgname=opencascade
pkgver=7.1.0
_commit=8ae442a8cbcd50d4c59a7024b198646123387579
pkgrel=1
epoch=1
pkgdesc="Open CASCADE Technology, 3D modeling & numerical simulation"
arch=('x86_64')
url="http://dev.opencascade.org/"
license=('custom')
depends=('tk' 'mesa' 'java-runtime' 'libxmu' 'ftgl' 'vtk' 'qtwebkit-tp')
makedepends=('java-environment' 'cmake')
options=('!libtool')
source=("opencascade-${pkgver}.tar.gz::http://git.dev.opencascade.org/gitweb/?p=occt.git;a=snapshot;h=${_commit};sf=tgz"
        'env.sh'
        'opencascade.sh'
        'opencascade.conf'
        'cmake.patch')
md5sums=('87b278a073f6e35cd7d3fb6ff425e061'
         'a96f28ee7f4273ae1771ee033a2a3af3'
         'd9368b8d348ced3ec4462012977552d2'
         '2924ecf57c95d25888f51071fdc72ad0'
         '0424cd6d6f8ce1a96b9883ef229b7048')

prepare() {
  cd occt-8ae442a
  
  patch -p1 -i ${srcdir}/cmake.patch
  #sed -i -e 's|-6.1||' adm/cmake/TKIVtkDraw/CMakeLists.txt
}
         
build() {
  cd occt-8ae442a
  mkdir -p build
  cd build
  
  #CXXFLAGS+=' -DvtkFloatingPointType=double'

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/${pkgname} \
    -DINSTALL_DIR=/opt/${pkgname} \
    -DCMAKE_INSTALL_RPATH=/opt/${pkgbase}/lib \
    -DINSTALL_FREETYPE=/opt/${pkgname} \
    -DINSTALL_TCL=/opt/${pkgname} \
    -D3RDPARTY_GL2PS_DIR= \
    -DUSE_VTK=ON
  make
}

package() {
  cd occt-8ae442a/build
  make DESTDIR=${pkgdir} install
  
  #cp -r src/UnitsAPI/ "${pkgdir}/opt/${pkgname}/src"
  install -D -m644 ${srcdir}/opencascade.conf ${pkgdir}/etc/ld.so.conf.d/opencascade.conf
  install -D -m 755 ${srcdir}/opencascade.sh ${pkgdir}/etc/profile.d/opencascade.sh
  install -m 755 ${srcdir}/env.sh ${pkgdir}/opt/${pkgname}
  
  install -dm755 $pkgdir/usr/share/licenses/${pkgname}/
  install -m644 ${srcdir}/${pkgname}-${pkgver}/LICENSE_LGPL_21.txt  ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/${pkgname}-${pkgver}/OCCT_LGPL_EXCEPTION.txt ${pkgdir}/usr/share/licenses/${pkgname}
}

