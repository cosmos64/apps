 
pkgname=opencascade
pkgver=6.9.1
pkgrel=2
epoch=1
pkgdesc="Open CASCADE Technology, 3D modeling & numerical simulation"
arch=('x86_64')
url="http://dev.opencascade.org/"
license=('custom')
depends=('tk' 'mesa' 'java-runtime' 'libxmu' 'ftgl' 'vtk')
makedepends=('java-environment')
options=('!libtool')
source=("http://files.opencascade.com/OCCT/OCC_${pkgver}_release/$pkgname-${pkgver}.tgz"
        'env.sh'
        'opencascade.sh'
        'opencascade.conf')
md5sums=('7f2e645b5010d190c9bb35dc457f9a7c'
         'a96f28ee7f4273ae1771ee033a2a3af3'
         'd9368b8d348ced3ec4462012977552d2'
         '2924ecf57c95d25888f51071fdc72ad0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  if [ ! -d $JAVA_HOME/include ]; then
    [ -e /etc/profile.d/openjdk6.sh ] && source /etc/profile.d/openjdk6.sh
    [ -e /etc/profile.d/jdk.sh ] && source /etc/profile.d/jdk.sh
  fi

  sed -i '/CSF_VTK_LIB/s/-6\.1//g' configure.ac
  sed -i -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.ac
  CXXFLAGS+=' -DvtkFloatingPointType=double'
  
  ./build_configure
  ./configure --disable-debug \
    --enable-production \
    --with-java-include=${JAVA_HOME}/include \
    --with-vtk-library=/usr/lib/ \
    --with-vtk-include=/usr/include/vtk/ \
    --prefix="$pkgdir/opt/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir/opt/$pkgname" install
  
  cp -r src/UnitsAPI/ "${pkgdir}/opt/$pkgname/src"
  install -D -m644 "${srcdir}/opencascade.conf" "${pkgdir}/etc/ld.so.conf.d/opencascade.conf"
  install -D -m 755 "${srcdir}/opencascade.sh" "${pkgdir}/etc/profile.d/opencascade.sh"
  install -m 755 "${srcdir}/env.sh" "${pkgdir}/opt/$pkgname"
  install -dm755 "$pkgdir/usr/share/licenses/$pkgname/"
  install -m644 LICENSE_LGPL_21.txt OCCT_LGPL_EXCEPTION.txt "$pkgdir/usr/share/licenses/$pkgname"
}
