
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=libkface
pkgver=${_kdever}
pkgrel=1
pkgdesc="Qt/C++ library to perform face detection and recognition over pictures"
url="http://www.digikam.org/"
arch=('x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('ki18n' 'opencv')
makedepends=('extra-cmake-modules' 'kdoctools')
#groups=('kde' 'kde-edu' 'kde-uninstall' 'education')
source=("http://download.kde.org/stable/applications/${_kdever}/src/${pkgname}-$_kdever.tar.xz"
        'https://github.com/KDE/libkface/commit/0684ee86b6510423bdd79325cd769e7a7c3d0e7c.diff')
md5sums=(`grep ${pkgname}-$_kdever.tar.xz ../kde-sc.md5 | cut -d" " -f1`
         'f9a71330409a5f0d250f1f7970db94f3')
#source=("git://anongit.kde.org/libkface.git")
#md5sums=('SKIP')

prepare() {
   cd $pkgname-${pkgver}
   sed -i -e 's|OPENCV_TEST_VERSION(3,1,0)|OPENCV_TEST_VERSION(3,2,0)|' src/recognition-opencv-lbph/facerec_borrowed.cpp
   sed -i -e 's|OPENCV_TEST_VERSION(3,1,0)|OPENCV_TEST_VERSION(3,2,0)|' src/recognition-opencv-lbph/facerec_borrowed.h
   patch -p1 -i ${srcdir}/0684ee86b6510423bdd79325cd769e7a7c3d0e7c.diff
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_PLUGINDIR=/usr/lib/qt5/plugins \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_OPENCV3=ON
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
