
pkgname=plasma-applet-redshift-control
pkgver=1.0.18
pkgrel=3
_commit=b45d454a6d0e8db022dd963cc50a59ccbda7f1c7
pkgdesc='Plasmoid for enabling and disabling redshift process, providing simple settings and mouse-wheel screen temperature control.'
arch=('x86_64')
url='https://www.linux-apps.com/content/show.php/Redshift+Control?content=170746'
license=('GPL')
depends=('plasma-desktop' 'redshift')
makedepends=('extra-cmake-modules')
conflicts=('redshift-plasmoid')
replaces=('redshift-plasmoid')
source=("https://github.com/kotelnik/plasma-applet-redshift-control/archive/v${pkgver}.tar.gz"
        'redshift1.12.patch')
md5sums=('5aab255007ba319fe9c4c73d45bb8d05'
         '84ac2e88d9b92e8306408e419190d41a')

prepare() {
  cd ${pkgname}-${pkgver}
  
  patch -p1 -i ${srcdir}/redshift1.12.patch
}

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON 
  make
}

package() {
  cd build
  
  make DESTDIR="$pkgdir" install
}
