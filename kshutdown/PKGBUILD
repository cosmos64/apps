
pkgname=kshutdown
pkgver=5.2
_pkgver=5.2
pkgrel=1
pkgdesc="Graphical shutdown utility that allows you to turn off or suspend a computer at a specified time."
url="https://kshutdown.sourceforge.io/"
arch=('x86_64')
license=('GPL')
depends=('kconfig' 'kconfigwidgets' 'kdbusaddons' 'kglobalaccel' 'ki18n' 'kidletime' 'knotifications'
         'knotifyconfig' 'kxmlgui')
makedepends=('extra-cmake-modules')
groups=('utils') 
source=("https://sourceforge.net/projects/kshutdown/files/KShutdown/${_pkgver}/kshutdown-source-${_pkgver}.zip")
sha256sums=('1ae0911b734a0d7eb12469ca95f066fb4d52c7880c2e9f1b692b236fd08337df')

build() {
  mkdir -p build
  cd build
  
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKS_KF5=TRUE
  make
}

package() {
  cd build
  
  make DESTDIR=${pkgdir} install
}
