
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname=kcm-touchpad
#pkgver=${_plasmaver}
pkgver=5.1.96
pkgrel=1
pkgdesc="KCM, daemon and applet for touchpad."
arch=('x86_64')
url="https://projects.kde.org/projects/kde/workspace/kcm-touchpad"
license=('GPL')
depends=('kinit' 'kcmutils' 'kcoreaddons' 'kconfigwidgets' 'kdbusaddons' 'kcompletion' 'kwidgetsaddons'
         'ki18n' 'kservice' 'knotifyconfig' 'kxmlgui' 'knotifications' 'kglobalaccel' 'plasma-workspace'
         'xf86-input-synaptics' 'libx11' 'libxcb' 'xorg-server' 'libxi')
makedepends=('extra-cmake-modules' 'kdoctools' 'pkg-config')
conflicts=('kde-touchpad-config')
replaces=('kde-touchpad-config')
groups=('system' 'kdeplasma5')
source=("git://anongit.kde.org/kcm-touchpad.git"
        'xlibbackend.diff'
        'x11.diff')
#md5sums=(`grep ${pkgname}-${_plasmaver}.tar.xz ../kf5.md5 | cut -d" " -f1`
md5sums=('SKIP'
         '417d582f137c4d6c8c80501c817e9f66'
         'bb1c795439814faebfad23f71161f544')

prepare() {
  cd ${pkgname}
  patch -p0 -i ${srcdir}/xlibbackend.diff
  patch -p0 -i ${srcdir}/x11.diff
}

build() {
  cd ${srcdir}
  
  mkdir -p build
  cd build

  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DQML_INSTALL_DIR=/usr/lib/qt5/qml \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt5/plugins \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}

